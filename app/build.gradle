apply plugin: 'com.android.application'

android {
    signingConfigs {
        release
    }
    compileSdkVersion 24
    buildToolsVersion '24.0.3'
    defaultConfig {
        applicationId 'com.landenlabs.all_uidemo'
        minSdkVersion 15
        targetSdkVersion 24
        versionName '1.06'
        versionCode 106

        // renderscriptTargetApi 23
        // renderscriptSupportModeEnabled true
        signingConfig signingConfigs.release
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                appendVersionNameVersionCode(variant, defaultConfig)
            }
            signingConfig signingConfigs.release
        }
        debug {
            renderscriptDebuggable true
        }
    }

    lintOptions {
        abortOnError false
        disable 'DefaultLocale', 'SimpleDateFormat', 'InflateParams', 'SpUsage',
                'UseCompoundDrawables', 'HandlerLeak', 'ViewConstructor', 'SetTextI18n',
                'RtlSymmetry', 'RtlHardcoded', 'ContentDescription',
                'MissingTranslation', 'ValidFragment', 'UnusedAttribute'
    }

    productFlavors {
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    sourceSets {
        main {
            renderscript.srcDirs = ['src']
            // res.srcDirs = ['res']
            // assets.srcDirs = ['assets']
        }
    }

    // Load signing config.
    def props = new Properties()
    props.load(new FileInputStream(rootProject.file("release.properties")))
    android.signingConfigs.release.storeFile rootProject.file(props.keyStore)
    android.signingConfigs.release.storePassword props.keyStorePassword
    android.signingConfigs.release.keyAlias props.keyAlias
    android.signingConfigs.release.keyPassword props.keyAliasPassword
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:appcompat-v7:24.2.1"
    compile "com.android.support:percent:24.2.1"
    compile "com.android.support:design:24.2.1"
    compile 'com.google.android.gms:play-services-analytics:9.6.1'
    compile 'net.hockeyapp.android:HockeySDK:4.1.1'
}

def appendVersionNameVersionCode(variant, defaultConfig) {
    variant.outputs.each { output ->
        if (output.zipAlign) {
            def file = output.outputFile
            def fileName = file.name.replace(".apk", "-v" + defaultConfig.versionCode + ".apk")
            def ourname =   project.parent.name;
            fileName = fileName.replace("app-", "$ourname-");
            output.outputFile = new File(file.parent, fileName)
        }

        def file = output.packageApplication.outputFile
        def fileName = file.name.replace(".apk", "-v" + defaultConfig.versionCode + ".apk")
        output.packageApplication.outputFile = new File(file.parent, fileName)
    }
}